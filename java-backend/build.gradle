plugins {
    id 'base'
    id 'checkstyle'
    id 'jacoco'
    id 'java'
    id 'pmd'
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

group = 'media.jambox'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport
test.dependsOn pmdTest
test.dependsOn pmdMain

tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compile 'se.michaelthelin.spotify:spotify-web-api-java:4.2.1'
    testImplementation 'junit:junit:4.13'
    testCompile "org.mockito:mockito-core:2.+"
    runtimeOnly project(':npm-frontend')
}
