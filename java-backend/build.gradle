plugins {
    id 'base'
    id 'checkstyle'
    id 'jacoco'
    id 'java'
    id 'pmd'
}

group = 'media.jambox'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'se.michaelthelin.spotify:spotify-web-api-java:4.2.1'
    testImplementation 'junit:junit:4.13'
    runtimeOnly project(':npm-frontend')
}
